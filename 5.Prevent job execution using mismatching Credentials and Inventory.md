# 5. Prevent job execution using mismatching Credentials and Inventory.

Story of another RFE, one of our customer wants to be able to enforce naming rules for example all prod inventory, credential and job template must be prefixed with `prod_` and only things with the same prefix can be use together.

This is a very reasonable and straight forward request and a simple way of reduce user error like using prod inventory on an job template intended for dev.

Currently in Ansible Automation Platform can not enforcing naming convention check. With the Policy as Code feature this is simple.

Example policy:

```rego
package aap_policy_examples

# inventory_prefix extracts the substring before the first underscore in `input.inventory.name`.
inventory_prefix = inv_pref if {
  parts := split(input.inventory.name, "_")
  inv_pref := parts[0]
}

# project_prefix extracts the substring before the first underscore in `input.project.name`.
project_prefix = proj_pref if {
  parts := split(input.project.name, "_")
  proj_pref := parts[0]
}

# credentials_prefixes is a list of prefix values from each credential's name.
credentials_prefixes = [cprefix |
  cred := input.credentials[_]                                # iterate over credentials
  parts := split(cred.name, "_")                              # split name
  cprefix := parts[0]                                         # grab the first part
]

# mismatch is true if either:
# 1. The inventory prefix != project prefix, OR
# 2. Any credential's prefix != project prefix.
mismatch if {
  inventory_prefix != project_prefix
}
mismatch if {
  some cp in credentials_prefixes
  cp != project_prefix
}

# violations is a single array with our message if there's a mismatch; otherwise empty.
violations = ["Mismatch prefix between Inventory, Credentials and Project detected."] if {
  mismatch
}
violations = [] if {
  not mismatch
}

# allowed is false if mismatch is true, otherwise true.
allowed = false if {
  mismatch
}
allowed = true if {
  not mismatch
}

# output is the final JSON structure we want to return.
output = {
  "allowed":    allowed,
  "violations": violations
}
```

Example input:

```json
{
  "id": 785,
  "name": "demo_Job Template",
  "credentials": [
    {
      "name": "demo_Credential",
      "description": "",
      "organization": null,
      "credential_type": 1,
      "managed": false,
      "inputs": {
          "username": "admin"
      },
      "kind": "ssh",
      "cloud": false,
      "kubernetes": false
    }
  ],
  "project": {
    "id": 6,
    "name": "demo_Project",
    "status": "successful",
    "scm_type": "git",
    "scm_url": "https://github.com/ansible/ansible-tower-samples",
    "scm_branch": "",
    "scm_refspec": "",
    "scm_clean": false,
    "scm_track_submodules": false,
    "scm_delete_on_update": false
  },
  "inventory": {
    "name": "prod_Inventory",
    "description": "",
    "has_active_failures": false,
    "total_hosts": 1,
    "hosts_with_active_failures": 0,
    "total_groups": 0,
    "has_inventory_sources": false,
    "total_inventory_sources": 0,
    "kind": ""
  },
}
```

Example output:

```json
{
  "allowed": false,
  "violations": [
    "Mismatch prefix between Inventory, Credentials and Project detected."
  ]
}
```
