# 3. Prevent job execution during maintenance window

Now that we are familiar with the basic of using an OPA policy to prevent job execution in Ansible Automation Platform lets dive in to a common use case: maintenance window.

For example, if you have a inventory of hosts that should only be updated during non working hours, let's say from 5PM to 9AM EST we can use an OPA policy to prevent job execution against that inventory outside of maintenance window.

Example policy:

```rego
package app_policy_examples

# Define maintenance window in EST (UTC-5)
maintenance_start_hour := 17 # 5 PM EST

maintenance_end_hour := 9 # 9 AM EST

utc_offset := 5 # EST is UTC-5

# Extract the job creation timestamp (which is in UTC)
job_created := input.created

ns := time.parse_rfc3339_ns(job_created)

created_clock := time.clock(ns) # returns [hour, minute, second]

created_hour_utc := created_clock[0]

# Convert UTC hour to EST
created_hour_est := (created_hour_utc - utc_offset) % 24

# Check if job was created within the maintenance window (EST)
is_maintenance_time if {
  created_hour_est >= maintenance_start_hour # After 5 PM EST
}

is_maintenance_time if {
  created_hour_est < maintenance_end_hour # Before 9 AM EST
}

# Default allow policy
default allow := true

allow := false if {
  not is_maintenance_time
}

# Generate violation message when denied
violations := ["No job execution allowed outside of maintenance window"] if {
  not is_maintenance_time
}
```

Example input provided by Ansible Automation Platform during query:

```json
{
  "id": 785,
  "name": "Demo Job Template",
  "created": "2025-02-27T20:32:14.874821Z",
  "created_by": {
    "id": 1,
    "username": "admin",
    "is_superuser": true
  }
}
```

Example output from policy query:

```rego
{
  "allowed": false,
  "violations": [
    "No job execution allowed outside of maintenance window"
  ]
}
```

When this policy is applied on a specific inventory all job execution using that inventory will ERROR outside of maintenance window.
