# 7a. Prevent pulling content from unapproved SCM repos used for AAP Projects

## Overview

Ansible Automation Platform uses the concept of Projects for provising automation content used for Job Templates and Workflows.

This example demonstrates how one can police the pulling of such content from only approved sources.

## Example Policy [aap_policy_examples/github_repo_validation.rego](aap_policy_examples/github_repo_validation.rego):

The following policy (`aap_policy_examples/github_repo_validation.rego`) provides this capability:

```rego
package aap_policy_examples

import rego.v1

# Define list of allowed GitHub repositories
allowed_github_repos := [
    "organization/repo1",
    "organization/repo2"
]

# Default policy response indicating allowed status with no violations
default github_repo_validation := {
    "allowed": true,
    "violations": [],
}

# Validate that the GitHub repository is in the whitelist
github_repo_validation := result if {
    # Extract SCM URL from input
    scm_url := object.get(input, ["project", "scm_url"], "")

    # Extract repository path from URL
    parts := split(scm_url, "/")
    count_parts := count(parts)
    org := parts[count_parts-2]
    repo_name := trim_suffix(parts[count_parts-1], ".git")
    repo_path := concat("/", [org, repo_name])

    # Check if repo path is not in the whitelist
    not repo_path in allowed_github_repos

    result := {
        "allowed": false,
        "violations": [sprintf("Repository '%v' is not in the allowed list", [repo_path])],
    }
}
```

## Enforcement Behavior

When applied at different enforcement points, this policy prevents job execution accordingly:

- **Job Template:** All jobs launched from the template will **ERROR**, preventing playbook execution.
- **Organization:** All jobs launch from job template that belongs to the organization will **ERROR**, preventing playbook execution.

## Try It Yourself

Take advantage of the [Rego playground](https://play.openpolicyagent.org/p/2XGSy8Lh05)!

## Real World Use Case: Only using approved Github repo sources

### Scenario  

Your company has only approved certain Github repositories to be used for automation purposes as these go through strict SDLC measures and controls. Under no circumstances should others like from personal accounts be used.

## Impact of Policy Enforcement in Ansible Automation Platform  

By applying the Block Policy, Ansible Automation Platform completely prevents automation execution at the specified enforcement points. 
This ensures that unvetted, non-approved automation content can be run.

## How AAP Enforces the Policy  

- ðŸš« **Job Templates Fail Immediately** â€“ Any playbook execution triggered by an affected job template will **ERROR**.  
- ðŸš« **Organization-Level Restrictions Apply** â€“ All jobs launched from job templates that belongs to a restricted organization will **ERROR**.  
